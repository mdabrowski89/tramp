apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs"
apply plugin: 'kotlin-kapt'

android {

    compileSdkVersion Versions.compiledSdk
    buildToolsVersion Versions.buildTools

    defaultConfig {
        project.ext.appName = 'TramP'
        applicationId "pl.mobite.tramp"
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode 1
        versionName "1.0"

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix "-dev"
            resValue "string", "app_name", "$project.ext.appName-dev"
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", project.ext.appName
        }
    }

    dexOptions {
        javaMaxHeapSize "3g"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':domain')

    implementation Dependencies.kotlinStdLib

    implementation Dependencies.appcompatLib
    implementation Dependencies.constraintLayoutLib
    implementation Dependencies.legacySupportLib

    implementation Dependencies.playServiceMapsLib

    implementation Dependencies.lifecycleExtensionsLib
    implementation Dependencies.lifecycleViewModelsLib

    implementation Dependencies.rxAndroidLib
    implementation Dependencies.rxKotlinLib
    implementation Dependencies.rxRelayLib

    implementation Dependencies.gsonLib

    implementation Dependencies.roomLib
    kapt Dependencies.roomCompiler

    implementation Dependencies.retrofitLib
    implementation Dependencies.retrofitGsonConverterLib
    implementation Dependencies.retrofitRxJava2AdapterLib
    implementation Dependencies.retrofitLoggingInterceptorLib

    implementation Dependencies.flexBoxLib

    // Navigation component
    implementation Dependencies.navigationFragmentLib
    implementation Dependencies.navigationUiLib

    // Unit tests
    testImplementation Dependencies.junitLib

    // UI tests
    androidTestImplementation Dependencies.testRunnerLib
    androidTestImplementation Dependencies.espressoLib
}

task changeApkName() {
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def date = new Date().format("yyMMdd_hhmmss")
            def fileName = "${project.ext.appName}_${variant.name}_${variant.versionName}_${date}.apk"
            output.outputFileName = fileName
        }
    }
}
gradle.taskGraph.whenReady {
    changeApkName
}

